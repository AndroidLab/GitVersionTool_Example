import org.json.JSONObject

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def getGitVersion = { ->
    def gitVersion = new ByteArrayOutputStream()
    exec {
        workingDir "../"
        commandLine 'dotnet', 'gitversion'
        standardOutput = gitVersion
    }
    return new JSONObject(gitVersion.toString())
}

def getVersionName = { ->
    return getGitVersion().getString("SemVer")
}

def getVersionCode = { ->
    def versionCode = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', 'HEAD', '--count'
        standardOutput = versionCode
    }
    return Integer.parseInt(versionCode.toString().trim())
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.alab.gitversiontool_example"
        minSdk 27
        targetSdk 32
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}